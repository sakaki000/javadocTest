
allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
}
subprojects {
    test {
        // 別タスクで一括で出力する
        reports.html.enabled = false

        jacoco {
            //execをrootprojectに出力する
            destinationFile = file("$rootProject.buildDir/jacoco/${project.name}.exec")
        }
    }
}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    reportOn subprojects.tasks.test
}

task jacocoRootReport(type: JacocoReport) {
    dependsOn = subprojects.test
    //レポート対象はmyprojectのソース
    additionalSourceDirs = files(project('first').sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(project('first').sourceSets.main.allSource.srcDirs)
    classDirectories = files(project('first').sourceSets.main.output)
    //レポートのためのexecはjacocoTestReportタスクの設定を利用
    executionData = files(subprojects.tasks.jacocoTestReport.executionData)
    reports {
        html.enabled = true
        xml.enabled = false
        csv.enabled = false
    }
}
